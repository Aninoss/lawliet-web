plugins {
    id 'org.springframework.boot' version '2.5.6'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'com.vaadin' version '0.14.3.7'
    id 'com.palantir.docker' version '0.30.0'
}

group = 'xyz.lawlietbot'
version = 'latest'
sourceCompatibility = 11
targetCompatibility = 11

repositories {
    mavenLocal()
    maven { url 'https://jitpack.io' }
    maven { url = uri('https://repo.maven.apache.org/maven2/') }
    maven { url = uri('https://maven.vaadin.com/vaadin-addons') }
    maven {
        url 'https://repo.maven.apache.org/maven2'
        name 'Maven Central'
    }
}

ext {
    set('vaadinVersion', "14.4.5")
}

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}

dependencies {
    implementation('com.vaadin:vaadin-spring-boot-starter:21.0.3') {
//         Webjars are only needed when running in Vaadin 13 compatibility mode
        ["com.vaadin.webjar", "org.webjars.bowergithub.insites",
         "org.webjars.bowergithub.polymer", "org.webjars.bowergithub.polymerelements",
         "org.webjars.bowergithub.vaadin", "org.webjars.bowergithub.webcomponents"]
                .forEach { group -> exclude(group: group) }
    }
    developmentOnly 'org.springframework.boot:spring-boot-devtools:2.5.5'
    testImplementation('org.springframework.boot:spring-boot-starter-test:2.5.5') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }

    implementation 'ch.qos.logback:logback-classic:1.2.6'
    implementation 'org.apache.logging.log4j:log4j-to-slf4j:2.14.1'
    implementation 'org.json:json:20210307'
    implementation 'com.google.guava:guava:31.0.1-jre'
    implementation 'com.github.Aninoss:Java-WebSocket:94609ebb95'
    implementation 'com.squareup.okhttp3:okhttp:4.9.2'
    implementation 'com.github.appreciated:vaadin-css-grid:2.0.0'
    implementation 'com.vaadin:vaadin-charts-flow:21.0.3'
    implementation 'com.stripe:stripe-java:20.85.0'
    implementation 'com.github.Aninoss:lawliet-dashboard-components:86e2bc4678'
}

dependencyManagement {
    imports {
        mavenBom "com.vaadin:vaadin-bom:${vaadinVersion}"
    }
}

test {
    useJUnitPlatform()
}

vaadin {
    pnpmEnable = true
}

docker {
    name 'registry.lawlietbot.xyz/lawliet-web:latest'
    files 'build/libs'
    tag 'DockerHub', "registry.lawlietbot.xyz/lawliet-web:latest"
}